apply plugin: 'com.android.library'

def sdkVersionName = '1.0.1'
def sdkVersionCode = 1

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode sdkVersionCode
        versionName sdkVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation rootProject.ext.playServicesAds

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
}

task createJar(type: Copy) {
    from('build/intermediates/aar_main_jar/release')
    into('../libs/')
    include('classes.jar')
    rename('classes.jar', 'adserver-sdk.jar')
}

build.finalizedBy createJar

apply plugin: 'maven-publish'

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                from components.release
                groupId 'adserver-online'
                artifactId 'android-sdk'
                version sdkVersionName
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = property('btUser')
    key = property('btKey')
    override = true
    publish = true
    publications = ['aar']
    pkg {
        userOrg = 'adserver-online'
        repo = 'maven'
        name = 'android-sdk'
        websiteUrl = "https://adserver.online"
        licenses = ['MIT']
        labels = ['android', 'advertising', 'sdk', 'adserver', 'ads', 'development']
        version {
            name = sdkVersionName
            vcsTag = sdkVersionName
            released = new Date()
        }
    }
}
